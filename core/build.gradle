project(":core") {
	description = 'Core modules'

	test.dependsOn { subprojects.test }
}

project(":core:api") {
	dependencies {
		compile lib.juz_commons_api
	}

	check {
		//check commons and here we have same spring parent bom  
		def dependency = project.dependencies.create(lib.juz_commons_api + "@pom")
		def configuration = project.configurations.detachedConfiguration(dependency)
		def pomFile = configuration.resolve().iterator().next()
		def commonsPom = new XmlSlurper().parse(pomFile)
		def platformBomDependency = commonsPom.dependencyManagement.dependencies.dependency
		assert platformBomDependency.artifactId == 'platform-bom'
		assert platformBomDependency.version == spring_platform_version: "Spring bom versions conflict! Commons platform version: $platformBomDependency.version"
	}
}


project(":core:base") {
	dependencies {
		compile project(":core:api")
		compile lib.juz_commons_infrastructure
	}
}

project(":core:config") {
	dependencies {
		compile project(":core:base")

		compile lib.hikaricp
		compile lib.postgresql
	}
}

project(":core:domain") {
	dependencies {
		compile project(":core:config")
		compile lib.gson

		testCompile lib.juz_test_base
	}
}

project(":core:webapp") {
	apply plugin: 'spring-boot'

	dependencies {
		compile project(":core:domain")

		compile lib.spring_boot_web
		compile lib.spring_boot_web_jetty
		compile lib.spring_boot_web_actuator

		compile lib.spring_security_config
		compile lib.spring_security_web

		compile lib.aspectjweaver

		testCompile lib.juz_test_base
	}

	springBoot {
		mainClass = "org.juz.seed.webapp.BootApplication"
	}
}